# EngagePatient.txt runs when you click on a patient to speak to them
#
# PatientPreCanvas is where the speak to patient button is held
# JoystickCanvas has the Joystick and is turned off when trying to speak to a patient
# $PC_Info is the Patient Information button that gives basic info about the current patient
# 
# $Pre_Speak - the button to speak to the patient
# NEWS_Nurse/PatientConversation.txt runs the dialogue between the player and the patient
# $Pre_End is a button that closes the PatientPreCanvas
#
# $GlobalVar is the empty game object that holds all of the global variables in the scene
# $Stage is the current stage number

/PatientPreCanvas on
JoystickCanvas off

If
	$Oriented = true
Then
	$Patient headturn Player
Endif

Label 'PCLoop_Stage01'
	If
		$PC_Info pressed
	Then
	$PC_Info reset
		If
			PatInfoCanvas isoff
		Then
			PatInfoCanvas on
			$Pat_Name write $PatientName
			$Pat_IC write $PatientIC
			$Pat_Diagnosis write $PatientDiag
			$Pat_Profile write $PatientProf
			$Pat_Sum write $PatientSummary
		Else
			PatInfoCanvas off
		Endif
	Endif

	If
		$Pat_Back pressed
	Then
		$Pat_Back reset
		PatInfoCanvas off
	Endif

	If
		$Pre_Speak pressed
	Then
		$Pre_Speak reset
		/PatientPreCanvas off
		Prompt ''
		If
			PatInfoCanvas ison
		Then
			$PC_Info reset
			PatInfoCanvas off
		Endif

		Do 'NEWS_Nurse/PatientConversation.txt'
		
		If
			$Stage = 1
		Then
			Do 'StageCheck.txt'
		Endif
		$Patient reset
		If
			$Oriented = true
		Then
			$Patient release
		Endif
		Return
	Endif

	If
		$Pre_End pressed
	Then
		$Pre_End resetPressed
		/PatientPreCanvas off
		If
			$Oriented = true
		Then
			$Patient release
		Endif
		Return
	Endif
Goto 'PCLoop_Stage01'